1000 REM BASIC PONG
1010 REM ---------- 
1020 CLS
1030 PRINT "******** BASIC PONG ********"
1040 PRINT "BAL JATEKOS     JOBB JATEKOS"
1050 PRINT "  FEL:  Q          FEL:  O"
1060 PRINT "  LE:   A          LE:   L"
1070 PRINT "  STOP: S          STOP: K"
1080 PRINT "****************************"
1090 PRINT
2000 LET X = 10
2010 LET Y = 3
2020 LET U = 1
2030 LET V = 1
2040 LET P = 11
2050 LET R = 0
2060 LET Q = 11
2070 LET S = 0
2080 LET W = 1
2090 FOR I = 11 TO 14
2100 PLOT 0, I, "white"
2110 PLOT 24, I, "white"
2120 NEXT I
3000 PAUSE 25 + 50 * (5 - LEVEL) 
3010 LET C = GETCHAR()
3020 IF C = "" THEN GOTO 4000
3030 IF C = "Q" OR C = "q" THEN LET R = 1
3040 IF C = "S" OR C = "s" THEN LET R = 0
3050 IF C = "A" OR C = "a" THEN LET R = -1
3060 IF C = "O" OR C = "o" THEN LET S = 1
3070 IF C = "K" OR C = "k" THEN LET S = 0
3080 IF C = "L" OR C = "l" THEN LET S = -1
3090 GOTO 3000
4000 PLOT X, Y, "gray"
4010 LET X = X + V
4020 LET Y = Y + U * W
4030 PLOT X, Y, "white"
4040 LET Z = ROUND(Y)
4050 IF Z <= 1 OR Z >= 23 THEN LET U = -U
4060 IF X <> 1 AND X <> 23 THEN GOTO 5000
4070 IF X = 1 AND (Z < P - 1 OR Z > P + 4) THEN GOTO 6000
4080 IF X = 23 AND (Z < Q - 1 OR Z > Q + 4) THEN GOTO 6000
4090 LET V = -V
4100 LET W = 0.9 + RAND(0.6) 
5000 IF P = 21 AND R = 1 THEN LET R = 0
5010 IF P = 0 AND R = -1 THEN LET R = 0
5020 IF Q = 21 AND S = 1 THEN LET S = 0
5030 IF Q = 0 AND S = -1 THEN LET S = 0
5040 IF R <> 1 THEN GOTO 5200
5050 PLOT 0, P, "gray"
5060 LET P = P + R
5070 PLOT 0, P + 3, "white"
5200 IF R <> -1 THEN GOTO 5300
5210 PLOT 0, P + 3, "gray"
5220 LET P = P + R
5230 PLOT 0, P, "white"
5300 IF S <> 1 THEN GOTO 5400
5310 PLOT 24, Q, "gray"
5320 LET Q = Q + S
5330 PLOT 24, Q + 3, "white"
5400 IF S <> -1 THEN GOTO 5500
5410 PLOT 24, Q + 3, "gray"
5420 LET Q = Q + S
5430 PLOT 24, Q, "white"
5500 GOTO 3000
6000 CLT
6010 PRINT "******** BASIC PONG ********"
6020 PRINT
6030 PRINT "******** JATEKVEGE *********"
6040 IF X = 1 THEN PRINT "JOBB"; ELSE PRINT "BAL";
6050 PRINT " JATEKOS NYERT!"
6060 PRINT "****************************"
